SCN PrefixerScript

array_var aFormTypes
array_var aTypeArray
ref rItem
string_var sPlugin
string_var sNewName
short iCount

BEGIN MenuMode
	IF GetGameRestarted	
		FOREACH aFormTypes <- sv_Split (GetINIString "Settings\FormTypes" "StarlightSkull\Prefixer.ini") ","
			iCount = 0
			FOREACH aTypeArray <- GetLoadedTypeArray (#*aFormTypes)
				rItem = *aTypeArray
				IF EVAL IsFormValid rItem
					; split the plugin name from the ref
					sPlugin = RefToString rItem
					sPlugin = sPlugin[0:((sv_Find ":" sPlugin) - 1)]
					sPlugin = "Plugins\" + sPlugin

					; if an INIstring exists add a space else set an empty one
					sNewName = GetINIString $sPlugin "StarlightSkull\Prefixer.ini"
					IF EVAL sv_Length sNewName
						sNewName += " "
					ELSE
						SetINIString $sPlugin "%e" "StarlightSkull\Prefixer.ini"
					ENDIF
					sNewName += $rItem

					; if formtype is challenge and is recurring
					IF EVAL (#*aFormTypes) == 113
						IF EVAL GetBit (GetChallengeFlags rItem), 1
							sNewName += " " + (GetINIString "Settings\Recurring" "StarlightSkull\Prefixer.ini")
						ENDIF
					ENDIF

					SetnameEx $sNewName rItem
					iCount += 1
				ENDIF
			LOOP
			Print $iCount + " forms of type " + *aFormTypes + " prefixed"
		LOOP
		sv_Destruct sPlugin, sNewName
		StopQuest PrefixerQuest
	ENDIF
END